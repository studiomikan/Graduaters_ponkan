# タイトル画面
*title_start
;historyopt output: false, enabled: false
;rclick enabled: false
;image lay: "base", file: "image/bg-black.png", x: 0, y: 0, visible: true

*title_return_point
;pretrans
;image lay: "base", file: "image/bg-black.png", x: 0, y: 0, visible: true
;trans time: 1000
;waittrans

;loadsound buf: "system1", file: "sound/se00.ogg"
;loadsound buf: "system2", file: "sound/se01.ogg"
;soundopt  buf: "system1", volume: 1, loop: false
;soundopt  buf: "system2", volume: 1, loop: false

# タイトル描画
## 背景
;pretrans
;image lay: 0, file: "image/bg-title.png"
;call label: "draw_version"

## ボタン
;freeimage lay: "2"
;clearcolor lay: "2"
;clearbuttons lay: "2"
;layopt lay: "2", visible: true, width: 1280, height: 720, x: 0, y: 0
;imagebutton lay: "2", x: 1280-270, y: 200+80*0, imagefile: "image/btn-newgame.png",  label: "newgame",  enterbuf: "system1"
;imagebutton lay: "2", x: 1280-270, y: 200+80*1, imagefile: "image/btn-loadgame.png", label: "loadgame", enterbuf: "system1"
;imagebutton lay: "2", x: 1280-270, y: 200+80*2, imagefile: "image/btn-config.png",   label: "config",   enterbuf: "system1"
;imagebutton lay: "2", x: 1280-270, y: 200+80*3, imagefile: "image/btn-gallery.png",  label: "gallery",  enterbuf: "system1"
;imagebutton lay: "2", x: 1280-270, y: 200+80*4, imagefile: "image/btn-exit.png",     label: "exit",     enterbuf: "system1"
;lockbuttons
;waitmove canskip: false

## Trans
;trans time: 700
;waittrans canskip: false

# ボタンロック解除
*unlockbuttons
;unlockbuttons
;s


# Load Game
*loadgame
;lockbuttons
;soundopt buf: "system2", volume: 1, loop: false
;playsound buf: "system2", loop: false
;call file: "saveload/load.pon"
;jump label: "unlockbuttons"

# Config
*config
;lockbuttons
;soundopt buf: "system2", volume: 1, loop: false
;playsound buf: "system2", loop: false
;call file: "config/config.pon"
;historyopt output: false, enabled: false
;rclick enabled: false
;jump label: "unlockbuttons"

# Gallery
*gallery
;lockbuttons
;soundopt buf: "system2", volume: 1, loop: false
;playsound buf: "system2", loop: false
;jump file: "gallery/gallery.pon"

# Exit
*exit
;lockbuttons
;soundopt buf: "system2", volume: 1, loop: false
;playsound buf: "system2", loop: false
-if (tv.system == null) tv.system = {};
-tv.system.dialogMessage = "ゲームを終了しますか？";
;call file: "dialog/confirm_dialog.pon", label: "show_dialog"
;if exp: "tv.system.dialogResult"
  ---
  try {
    window.close();
  } catch (e) {
    console.error(e);
  }
  ---
;endif
;jump label: "unlockbuttons"


# New Game
*newgame
;lockbuttons
;soundopt buf: "system2", volume: 1, loop: false
;playsound buf: "system2", loop: false

;pretrans
;layopt lay: "all", exclude:"system", visible: false
;freeimage lay: "all", exclude:"system"
;clearbuttons lay: "all", exclude:"system"
;clearcolor lay: "all", exclude:"system"
;clear lay: "all", exclude:"system"
;call file: "script/init_system2.pon"

## ホワイトアウト
;layopt lay: 0, x: 0, y: 0, width: 1280, height: 720, visible: true
;image lay: "base", file: "image/bg-white.png"
;trans time: 2000
;waittrans canskip: true

## ブラックアウト
;pretrans
;layopt lay: 0, x: 0, y: 0, width: 1280, height: 720, visible: true
;image lay: "base", file: "image/bg-black.png"
;trans time: 2000
;waittrans canskip: true
;wait time: 2000, canskip: false

;historyopt output: true, enabled: true
;rclick enabled: true, jump: false, call: false

;jump file: "scenario/01.pon"
;s


# バージョン番号を出す
*draw_version
;messageopt {
  lay: "base",
  fontfamily: ["GenShinGothic", "monospace"],
  fontsize: 16,
  weight: "normal",
  color: "0xFFFFFF",
  pitch: 0,
  lineheight: 16,
  linepitch: 5,
  align: "left",
  shadow: false,
  edgewidth: 0,
  autoreturn: false,
  margint: 0, marginr: 0, marginb: 0, marginl: 0
};
;clear lay: "base"
;locate lay: "base", x: 10, y: 720 - 26
;ch lay: "base", text: "&'Version ' + ponkan.config.gameVersion"
;return
