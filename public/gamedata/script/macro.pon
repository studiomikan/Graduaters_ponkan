#-------------------------------------------------------------------
# マクロ定義
#-------------------------------------------------------------------

;macro name: "systembutton"
  ;layopt lay: "&tv.layer.systembutton", visible: "&mp.visible"
;endmacro

;macro name: "show_meslay"
  ;layopt lay: "mes", visible: true
  ;layopt lay: "&tv.layer.systembutton", visible: true
;endmacro

;macro name: "hide_meslay"
  ;layopt lay: "mes", visible: false
  ;layopt lay: "&tv.layer.systembutton", visible: false
;endmacro

#-------------------------------------------------------------------
# メッセージレイヤ等をオレンジ/青に設定する
# 操作対象はデフォルトで表ページ
#-------------------------------------------------------------------
;macro name: "set_to_orange"
  -tv.meslay_mode = "orange";
  -tv.page = mp.page == "back" ? "back" : "fore";
  ;currentpage page: "&tv.page"
  ;layopt lay: "mes", page: "&tv.page", x: 0, y: (720-200)
  ;clearcolor lay: "mes", page: "&tv.page"
  ;image lay: "mes", page: "&tv.page", file: "image/message-base1.png"
  ;mesopt {
    lay: "mes",
    page: "&tv.page",
    marginl: (1280-800)/2,
    marginr: (1280-800)/2,
    margint: 65,
  }
  ;call file: "script/system_button.pon"
;endmacro

;macro name: "set_to_blue"
  -tv.meslay_mode = "blue";
  -tv.page = mp.page == "back" ? "back" : "fore";
  ;currentpage page: "&tv.page"
  ;layopt lay: "mes", page: "&tv.page", x: 0, y: (720-200)
  ;clearcolor lay: "mes", page: "&tv.page"
  ;image lay: "mes", page: "&tv.page", file: "image/message-base2.png"
  ;mesopt {
    lay: "mes",
    page: "&tv.page",
    marginl: (1280-790)/2,
    marginr: (1280-790)/2,
    margint: 65,
  }
  ;call file: "script/system_button.pon"
;endmacro

;macro name: "set_to_radio_orange"
  -tv.meslay_mode = "radio_orange";
  -tv.page = mp.page == "back" ? "back" : "fore";
  ;currentpage page: "&tv.page"
  ;layopt lay: "mes", page: "&tv.page", x: (1280-640)/2, y: (720-360)/2
  ;clearcolor lay: "mes", page: "&tv.page"
  ;image lay: "mes", page: "&tv.page", file: "image/message-base3.png"
  ;mesopt lay: "mes", page: "&tv.page", margint: 10, marginr: 30, marginb: 10, marginl: 15, lineheight: 30,
  ;call file: "script/system_button.pon"
;endmacro

;macro name: "set_to_radio_blue"
  -tv.meslay_mode = "radio_blue";
  -tv.page = mp.page == "back" ? "back" : "fore";
  ;currentpage page: "&tv.page"
  ;layopt lay: "mes", page: "&tv.page", x: (1280-640)/2, y: (720-360)/2
  ;clearcolor lay: "mes", page: "&tv.page"
  ;image lay: "mes", page: "&tv.page", file: "image/message-base4.png"
  ;mesopt lay: "mes", page: "&tv.page", margint: 10, marginr: 30, marginb: 10, marginl: 15, lineheight: 30,
  ;call file: "script/system_button.pon"
;endmacro


#-------------------------------------------------------------------
# 発言者の名前
#-------------------------------------------------------------------
;macro name: "echo_name"
  ;clear lay: "speaker"
  ;ch lay: "speaker", text: "&mp.name"
  ;layopt lay: "speaker", visible: true
;endmacro

;macro name: "noname"
  ;clear lay: "speaker"
  -tv.speaking = false;
;endmacro

;macro name: "joban"
  ;clear lay: "speaker"
  -tv.speaking = false;
  #-tv.speaking = true;
  #;ch text: "「"
  #;indent
;endmacro

;macro name: "kenji"
  ;echo_name name: "健二"
  -tv.speaking = true;
  ;ch text: "「"
  ;indent
;endmacro

#;macro name: "asuka"
#  ;echo_name name: "明日香"
#;endmacro
#
#;macro name: "yuuki"
#  ;echo_name name: "優希"
#;endmacro


#-------------------------------------------------------------------
# 行末
#-------------------------------------------------------------------
;macro name: "endpage"
  ;if exp: "tv.speaking"
    ;ch text: "」"
    ;endindent
  ;endif
  ;p
  ;clear
;endmacro


#-------------------------------------------------------------------
# メッセージレイヤー表示切り替え
#-------------------------------------------------------------------
;macro name: "fadein_meslay"
  ;clear lay: "mes"
  ;clear lay: "speaker"
  ;pretrans
  ;show_meslay
  ;noname
  ;trans time: "200"
  ;waittrans canskip: "false"
;endmacro

;macro name: "fadeout_meslay"
  ;pretrans
  ;hide_meslay
  ;noname
  ;trans time: "200"
  ;waittrans canskip: "false"
;endmacro


#-------------------------------------------------------------------
# 演出 ミニテロップイン -> アウト
#-------------------------------------------------------------------
;macro name: "minitelop"
  ;layopt lay: "all", visible: false
  # イン
  ;image lay: "telop", file: "image/logo-title-mini.png", x: 788+20, y: 562, visible: true
  ;layopt lay: "telop", alpha: 0
  ;move lay: "telop", time: 500, path: [{x: 788, y: 562, alpha: 1}], ease: "out"
  ;waitmove canskip: false
  ;wait time: 1000, canskip: false
  # アウト
  ;move lay: "telop", time: 500, path: [{x: 788-20, y: 562, alpha: 0}], ease: "out"
  ;waitmove canskip: false
  ;freeimage lay: "telop"
;endmacro

#-------------------------------------------------------------------
# 演出 テロップにフェード -> ブラックアウト
#-------------------------------------------------------------------
;macro name: "telop"
  # フェード
  ;pretrans
  ;layopt lay: "all", visible: false
  ;image lay: "telop", file: "image/logo-title-mini.png", x: 788+20, y: 562, visible: true
  ;trans time: 1000
  ;waittrans canskip: false
  ;wait time: 1500, canskip: false
  # アウト
  ;pretrans
  ;layopt lay: "telop", visible: false
  ;trans time: 1000
  ;waittrans canskip: false
  ;wait time: 1500, canskip: false
;endmacro

#-------------------------------------------------------------------
# 演出 ラジオ
#-------------------------------------------------------------------
;macro name: "radio_mes_jump1"
  ;move {
    lay: "mes",
    time: "75",
    ease: "out",
    path: [
      {x: (1280-640)/2, y: (720-360)/2 - 75, alpha: 1},
      {x: (1280-640)/2, y: (720-360)/2, alpha: 1},
      {x: (1280-640)/2, y: (720-360)/2 - 75, alpha: 1},
      {x: (1280-640)/2, y: (720-360)/2, alpha: 1},
    ],
  }
;endmacro

;macro name: "radio_mes_combo1"
  ;move {
    lay: "mes",
    time: "250",
    ease: "out",
    type: "catmullrom",
    path: [
      {x: 50, y: 50, alpha: 1},
      {x: 1280-640-50, y: 50, alpha: 1},
      {x: 50, y: 720-315-50, alpha: 1},
      {x: 1280-640-50, y: 720-360-50, alpha: 1},
      {x: (1280-640)/2, y: (720-360)/2, alpha: 1},
    ],
  }
;endmacro

;macro name: "radio_mes_top"
  ;move {
    lay: "mes",
    time: "200",
    ease: "out",
    type: "liner",
    path: [ {x: (1280-640)/2, y: (720-360)/2-50, alpha: 1} ],
  }
;endmacro

;macro name: "radio_mes_right"
  ;move {
    lay: "mes",
    time: "200",
    ease: "out",
    type: "liner",
    path: [ {x: 1280-640-50, y: (720-360)/2, alpha: 1} ],
  }
;endmacro

;macro name: "radio_mes_bottom"
  ;move {
    lay: "mes",
    time: "200",
    ease: "out",
    type: "liner",
    path: [ {x: (1280-640)/2, y: 720-360-50, alpha: 1} ],
  }
;endmacro

;macro name: "radio_mes_left"
  ;move {
    lay: "mes",
    time: "200",
    ease: "out",
    type: "liner",
    path: [ {x: 50, y: (720-360)/2, alpha: 1} ],
  }
;endmacro

;macro name: "radio_mes_center"
  ;move {
    lay: "mes",
    time: "200",
    ease: "out",
    type: "liner",
    path: [ {x: (1280-640)/2, y: (720-360)/2, alpha: 1} ],
  }
;endmacro



#-------------------------------------------------------------------
# 効果音再生
#-------------------------------------------------------------------
;macro name: "playse"
  ---
    tv.file = "sound/se" + mp.num + ".ogg";
    tv.loop = mp.loop != null ? mp.loop : false;
  ---
  ;loadsound buf: "se", "file": "&tv.file"
  ;soundopt buf: "se", volume: 1, loop: "&tv.loop"
  ;playsound buf: "se"
;endmacro

;macro name: "stopse"
  ;stopsound buf: "se"
;endmacro

#-------------------------------------------------------------------
# BGM再生
#-------------------------------------------------------------------
;macro name: "playbgm"
  ---
    tv.file = "sound/bgm" + mp.num + ".ogg";
  ---
  ;loadsound buf: "bgm", "file": "&tv.file"
  ;soundopt buf: "bgm", volume: 1, loop: true
  ;playsound buf: "bgm"
;endmacro

;macro name: "stopbgm"
  ;stopsound buf: "bgm"
;endmacro

;macro name: "fadeoutbgm"
  ;fadeoutsound buf: "bgm", time: 1000
;endmacro


#-------------------------------------------------------------------
# キャラクター表示関係
#-------------------------------------------------------------------

# キャラクターをロード
# name: 名前。"asuka" or "yuuki"
# num: 画像の番号。1～5
# visible: 表示非表示。省略時はtrue
;macro name: "loadchara"
  - console.log("loadchara", mp);
  ---
    tv.file = "image/chara/" + mp.name + "0" + mp.num + ".png";
    tv.visible = mp.visible != null ? mp.visible : true;
    switch (mp.name) {
      case "asuka": tv.lay = "asuka"; break;
      case "yuuki": tv.lay = "yuuki"; break;
      default: tv.lay = "asuka"; break;
    }
  ---
  ;image lay: "&tv.lay", file: "&tv.file", x: (1280-467)/2, y: 20, visible: "&tv.visible"
;endmacro

# 立ち絵を表示
# num: 画像の番号。1～5
;macro name: "chara_in"
  ---
    var lay = tv.lay = mp.name;
    var current = ponkan.getLayers({lay: lay})[0].imageFilePath;
    if (current == null) {
      tv.action = "fadein";
    } else {
      var reg = null;
      if (mp.name == "asuka") {
        reg = /asuka[0-9][0-9]\.png/;
      } else if (mp.name == "yuuki") {
        reg = /yuuki[0-9][0-9]\.png/;
      }
      var match = current.match(reg);
      if (match == null) {
        tv.action = "fadein";
      } else {
        var newFile = mp.name + "0" + mp.num + ".png";
        if (newFile == match[0]) {
          tv.action = "none"; // 現在表示している立ち絵のままでOKなので何もしない
        } else {
          tv.action = "change";
        }
      }
    }
    console.log("chara_in action:", tv.action);
  ---
  ;if exp: "tv.action == 'change'"
    # 立ち絵入れ替え
    ;pretrans
    ;loadchara name: "&mp.name", num: "&mp.num", visible: true
    ;trans time: 200
    ;waittrans
  ;elsif exp: "tv.action == 'fadein'"
    # 新規フェードイン
    ;pretrans
    ;loadchara name: "&mp.name", num: "&mp.num", visible: true
    ;trans time: 500
    ;waittrans
  ;endif
;endmacro

# 立ち絵をフェードアウト
;macro name: "chara_out"
  ---
    tv.lay = mp.name;
  ---
  ;pretrans
  ;layopt lay: "&tv.lay", visible: false
  ;trans time: 500
  ;waittrans
;endmacro

# 立ち絵を即アウト
;macro name: "unloadchar"
  ---
    tv.lay = mp.name;
  ---
  ;layopt lay: "&tv.lay", visible: false
;endmacro

# 明日香の立ち絵と名前を表示
;macro name: "asuka"
  ;if exp: "mp.num != null"
    ;chara_in name: "asuka", num: "&mp.num"
  ;endif
  ;echo_name name: "明日香"
  -tv.speaking = true;
  ;ch text: "「"
  ;indent
;endmacro

# 明日香の立ち絵の名前を非表示に
;macro name: "asuka_out"
  ;noname
  ;chara_out name: "asuka"
;endmacro

# 優希の立ち絵と名前を表示
;macro name: "yuuki"
  ;if exp: "mp.num != null"
    ;chara_in name: "yuuki", num: "&mp.num"
  ;endif
  ;echo_name name: "優希"
  -tv.speaking = true;
  ;ch text: "「"
  ;indent
;endmacro

# 優希の立ち絵の名前を非表示に
;macro name: "yuuki_out"
  ;noname
  ;chara_out name: "yuuki"
;endmacro

# 優希を？？で表示
;macro name: "yuuki?"
  ;if exp: "mp.num != null"
    ;chara_in name: "yuuki", num: "&mp.num"
  ;endif
  ;echo_name name: "？？"
  -tv.speaking = true;
  ;ch text: "「"
  ;indent
;endmacro


#-------------------------------------------------------------------
# トランジション：ホワイトアウト
#-------------------------------------------------------------------
;macro name: "whiteout"
  ;pretrans
  ;freeimage lay: "base"
  ;clearcolor lay: "base"
  ;fillcolor lay: "base", color: "0xFFFFFF", alpha: 1
  ;layopt lay: "all", visible: false
  ;layopt lay: "base", visible: true
  ;trans time: 1500
  ;waittrans canskip: true
  ;stoptrans
;endmacro


#-------------------------------------------------------------------
# トランジション：ブラックアウト
#-------------------------------------------------------------------
;macro name: "blackout"
  ;pretrans
  ;freeimage lay: "base"
  ;clearcolor lay: "base"
  ;fillcolor lay: "base", color: "0x000000", alpha: 1
  ;layopt lay: "all", visible: false
  ;layopt lay: "base", visible: true
  ;trans time: 1500
  ;waittrans canskip: true
;endmacro

#-------------------------------------------------------------------
# 背景変更＆表示
#-------------------------------------------------------------------
;macro name: "cb"
  ;freeimage lay: "base"
  ;clearcolor lay: "base"
  ;if exp: "mp.bg == 'black' || mp.bg == 'white'"
    -tv.bgcolor = mp.bg == 'black' ? 0x000000 : 0xFFFFFF
    ;fillcolor lay: "base", color: "&tv.bgcolor", alpha: 1
  ;else
    ;image lay: "base", file: "&'image/bg-' + mp.bg + '.png'"
  ;endif
  ;layopt lay: "base", visible: true
;endmacro

#-------------------------------------------------------------------
# 背景変更：フェード
#-------------------------------------------------------------------
;macro name: "cb_fade"
  ;pretrans
  ;cb bg: "&mp.bg"
  ;trans time: 1000, method: "crossfade"
  ;waittrans "canskip": true
;endmacro


#-------------------------------------------------------------------
# 背景変更：移動
#-------------------------------------------------------------------
;macro name: "cb_move"
  ;pretrans
  ;cb bg: "&mp.bg"
  ;trans time: 1000, method: "univ", rule: "rule/move.png"
  ;waittrans "canskip": true
;endmacro


#-------------------------------------------------------------------
# 背景変更：時間経過
#-------------------------------------------------------------------
;macro name: "cb_time"
  ;pretrans
  ;cb bg: "&mp.bg"
  ;trans time: 1000, method: "univ", rule: "rule/time.png", vague: 128
  ;waittrans "canskip": true
;endmacro



;return
